#!/usr/bin/env zsh

# Open the git origin remote in the default browser
function gbr {
  local remote=$(git remote get-url origin 2>/dev/null)

  local host repo
  if [[ $remote =~ "^git@([^:]+):(.+)$" ]]; then
    host=${match[1]}
    repo=${match[2]%.git}
  elif [[ $remote =~ "^https://([^/]+)/(.+)$" ]]; then
    host=${match[1]}
    repo=${match[2]%.git}
  else
    print "Cannot parse remote 'origin': ${remote:-not found}" >&2
    return 1
  fi

  local url="https://$host/$repo"

  if [[ -n $1 ]]; then
    local rev=$(git rev-parse --abbrev-ref HEAD 2>/dev/null)
    [[ $rev == "HEAD" ]] && rev=$(git rev-parse HEAD 2>/dev/null)

    if [[ $host == *"github"* ]]; then
      [[ -f $1 ]] && url+="/blob/$rev/$1" || url+="/tree/$rev/$1"
    elif [[ $host == *"gitlab"* ]]; then
      [[ -f $1 ]] && url+="/-/blob/$rev/$1" || url+="/-/tree/$rev/$1"
    else
      print "Unsupported host: $host" >&2
      return 1
    fi
  fi

  xdg-open "$url"
}
